plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.11'
    id 'io.spring.dependency-management' version '1.1.7'
    id "com.diffplug.spotless" version "6.23.3"
}

group = 'kr.java'
version = '0.0.1-SNAPSHOT'
description = 'AIBE4_FinalProject_Team4'

spotless {
    java {
        target '**/*.java'
        // 구글 스타일(AOSP) = 들여쓰기 4칸 적용
        googleJavaFormat().aosp()

        importOrder() // import 순서 정의
        removeUnusedImports() // 안 쓰는 import 제거
        trimTrailingWhitespace() // 끝 공백 제거
        endWithNewline() // 파일 끝 빈 줄 추가
    }
    // [중요] 윈도우/맥 사용자 간 충돌 방지를 위해 LF로 통일
    lineEndings 'UNIX'
}

// 컴파일 시 자동으로 포맷팅 적용 (선택)
tasks.named('compileJava') {
    dependsOn 'spotlessApply'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

ext {
    set('springAiVersion', "1.1.2")
}

dependencies {
    // Spring Boot Core
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // Database & Cache
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.session:spring-session-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    runtimeOnly 'org.postgresql:postgresql'
    // Source: https://mvnrepository.com/artifact/io.hypersistence/hypersistence-utils-hibernate-63
    implementation 'io.hypersistence:hypersistence-utils-hibernate-63:3.15.2'

    // Spring AI & ETL & RAG
    implementation 'org.springframework.ai:spring-ai-starter-model-ollama'
    implementation 'org.springframework.ai:spring-ai-starter-model-openai'
    implementation 'org.springframework.ai:spring-ai-starter-model-google-genai'

    implementation 'org.apache.pdfbox:pdfbox:3.0.5'
    implementation('com.github.tabulapdf:tabula-java:master-SNAPSHOT') {
        exclude group: 'org.slf4j', module: 'slf4j-simple'
    }
    implementation 'org.apache.poi:poi:5.4.1'
    implementation 'org.apache.poi:poi-ooxml:5.4.1'
    implementation 'org.springframework.ai:spring-ai-tika-document-reader'

    implementation 'org.springframework.ai:spring-ai-rag'
    implementation 'org.springframework.ai:spring-ai-advisors-vector-store'
    implementation 'org.springframework.ai:spring-ai-starter-vector-store-pgvector'

    // Monitoring
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-influx'

    // Utils & Test
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.ai:spring-ai-bom:${springAiVersion}"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
